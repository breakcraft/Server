[label,pvm_dragon_longsword_sa]
~set_sa_vars(oc_param(inv_getobj(worn, ^wearpos_rhand), sa_energy));
def_int $damage = 0;
def_int $maxhit = scale(125, 100, %com_maxhit);
def_int $damage_capped = 0;
// https://archive.ph/QQRNa, rolled against npc slash def, but uses current damage type
def_int $attack_roll = ~player_attack_roll_specific(%damagetype);
def_int $defence_roll = ~npc_defence_roll_specific(^slash_style);
if (randominc($attack_roll) > randominc($defence_roll)) {
    if (npc_type = count_draynor & inv_total(inv, garlic) > 0) {
        $maxhit = add($maxhit, 1);
    }
    $damage = randominc(min($maxhit, npc_param(max_dealt)));
    $damage_capped = min($damage, npc_stat(hitpoints));
    ~give_combat_experience(%damagestyle, $damage_capped, %npc_combat_xp_multiplier);
    npc_heropoints($damage_capped);
}
anim(cleave, 0);
sound_synth(cleave, 0, 0);
spotanim_pl(sp_attack_cleave_spotanim, 96, 0);
~npc_retaliate(0);
npc_queue(2, $damage, 0);
npc_anim(npc_param(defend_anim), 20); 
if (npc_param(defend_sound) ! null) {
    sound_synth(npc_param(defend_sound), 0, 20); // delay npc this tick
}
